apply plugin: 'konan'

konan.targets = [ "android_arm64", "android_arm32"]

konanArtifacts {
  dynamic("knes") {
    enableMultiplatform true
  }
}

def outDir = project(':android:app').file("src/main/jniLibs")
def platforms = [
    "armeabi-v7a": [konanTarget: "android_arm32"],
    "arm64-v8a"  : [konanTarget: "android_arm64"]
]

task copyLibs(type: Copy) {
  dependsOn konanArtifacts.knes
  destinationDir outDir

  platforms.each { name, platform ->
    into(name) {
      from konanArtifacts.knes."${platform.konanTarget}".artifact
    }
  }
}

task deleteOut(type: Delete) {
  delete outDir
}

build {
  finalizedBy copyLibs
}

clean.dependsOn deleteOut

tasks.matching { it.name == 'preBuild' }.all {
  it.dependsOn copyLibs
}

dependencies {
  expectedBy project(':common')
}